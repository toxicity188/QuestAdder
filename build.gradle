plugins {
    id 'java'
    id 'org.jetbrains.kotlin.jvm' version '1.9.10'
    id 'com.github.johnrengelman.shadow' version '8.1.1'
    id 'maven-publish'
}

def questAdderGroup = 'kor.toxicity'
def questAdderVersion = '1.1.2-SNAPSHOT'

allprojects {
    apply plugin: 'java'
    apply plugin: 'org.jetbrains.kotlin.jvm'
    apply plugin: 'com.github.johnrengelman.shadow'
    apply plugin: 'maven-publish'

    group = questAdderGroup
    version = questAdderVersion

    repositories {
        mavenCentral()
        maven {
            name = "papermc-repo"
            url = "https://repo.papermc.io/repository/maven-public/"
        }
        maven {
            url = 'https://repo.dmulloy2.net/repository/public/'
        }
        maven {
            name = "sonatype"
            url = "https://oss.sonatype.org/content/groups/public/"
        }
        maven {
            name = 'citizens-repo'
            url = 'https://maven.citizensnpcs.co/repo'
        }
        maven {
            url = 'https://jitpack.io'
        }
        maven {
            name = "alessiodpRepo"
            url = uri("https://repo.alessiodp.com/releases")
        }
        maven {
            name = 'minecraft-repo'
            url = 'https://libraries.minecraft.net/'
        }
        maven {
            url = "https://maven.enginehub.org/repo/"
        }
        maven {
            url = 'https://mvn.lumine.io/repository/maven/'
        }
        maven {
            url = 'https://repo.bg-software.com/repository/api/'
        }
        maven {
            url = 'https://nexus.phoenixdevt.fr/repository/maven-public/'
        }
    }

    dependencies {
        testImplementation 'org.junit.jupiter:junit-jupiter-api:5.9.2'
        testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.9.2'
        testImplementation 'io.papermc.paper:paper-api:1.19.4-R0.1-SNAPSHOT'

        compileOnly 'io.papermc.paper:paper-api:1.19.4-R0.1-SNAPSHOT'
        compileOnly group: 'net.byteflux', name: 'libby-bukkit', version: '1.1.5'
        compileOnly group: 'com.github.oraxen', name: 'oraxen', version: '1.158.0'
        compileOnly group: 'com.github.LoneDev6', name: 'api-itemsadder', version: '3.5.0b'
        compileOnly 'net.citizensnpcs:citizens-main:2.0.32-SNAPSHOT'
        compileOnly "com.github.MilkBowl:VaultAPI:1.7"
        compileOnly 'com.mojang:authlib:1.5.21'
        compileOnly 'com.mojang:brigadier:1.0.18'
        compileOnly group: "com.comphenix.protocol", name: "ProtocolLib", version: "4.8.0"
        compileOnly 'com.sk89q.worldedit:worldedit-bukkit:7.3.0-SNAPSHOT'
        compileOnly 'com.sk89q.worldguard:worldguard-bukkit:7.1.0-SNAPSHOT'
        compileOnly fileTree("libs")
        compileOnly 'io.lumine:Mythic-Dist:5.3.5'
        compileOnly 'com.bgsoftware:SuperiorSkyblockAPI:2023.2'
        compileOnly 'net.Indyuce:MMOCore-API:1.12-SNAPSHOT'
        compileOnly 'net.Indyuce:MMOItems-API:6.9.5-SNAPSHOT'
        compileOnly 'io.lumine:MythicLib-dist:1.6.2-SNAPSHOT'
        compileOnly 'com.github.BeYkeRYkt.LightAPI:lightapi-bukkit-common:5.3.0-Bukkit'
    }
    tasks.named('test', Test) {
        useJUnitPlatform()

        maxHeapSize = '1G'

        testLogging {
            events "passed"
        }
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation project(path: ':nms:v1_17_R1', configuration: 'shadow')
    implementation project(path: ':nms:v1_18_R1', configuration: 'shadow')
    implementation project(path: ':nms:v1_18_R2', configuration: 'shadow')
    implementation project(path: ':nms:v1_19_R1', configuration: 'shadow')
    implementation project(path: ':nms:v1_19_R2', configuration: 'shadow')
    implementation project(path: ':nms:v1_19_R3', configuration: 'shadow')
    implementation project(path: ':nms:v1_20_R1', configuration: 'shadow')
    implementation project(path: ':nms:v1_20_R2', configuration: 'shadow')
    implementation project(path: ':plugin', configuration: 'shadow')
}

def targetJavaVersion = 17
java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release = targetJavaVersion
    }
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}
jar {
    finalizedBy shadowJar
}

shadowJar {
    relocate("kotlin","kor.toxicity.questadder.shaded.kotlin")
    relocate("com.ticxo.playeranimator","kor.toxicity.questadder.shaded.com.ticxo.playeranimator")
    relocate("org.apache.commons.io","kor.toxicity.questadder.shaded.org.apache.commons.io")
    relocate("net.objecthunter","kor.toxicity.questadder.shaded.net.objecthunter")
    relocate("org.zeroturnaround","kor.toxicity.questadder.shaded.org.zeroturnaround")
    relocate("org.bstats","kor.toxicity.questadder.shaded.org.bstats")
    dependencies {
        exclude(dependency("org.jetbrains:annotations:13.0"))
    }
}

kotlin {
    jvmToolchain(17)
}
